plugins {
    alias libs.plugins.fabric.loom
    alias libs.plugins.legacy.looming
    id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots" }
    // maven { url "https://jitpack.io" }
    // flatDir { dirs "libs" }
}

loom {
    decompilers {
        vineflower {
            options.putAll(["mark-corresponding-synthetics": "1", "indent-string": "    "])
        }
    }
    mixin {
        useLegacyMixinAp = false
    }
    // legacyLooming {
    //     intermediaryVersion = 1
    // }
    // runs.client.programArgs "--userProperties", "{}"
}

dependencies {
    minecraft libs.minecraft
    mappings legacy.yarn(libs.versions.minecraft.get(), libs.versions.yarn.build.get())
    modImplementation libs.fabric.loader
    vineflowerDecompilerClasspath libs.vineflower
}

processResources {
    filesMatching "fabric.mod.json", {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release.set(8)
    }
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

jar {
    from "LICENSE"
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
